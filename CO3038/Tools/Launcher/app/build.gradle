apply plugin: 'com.android.application'
if (enable_plugin_kotlin) {
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
}

android {
    compileSdkVersion version_setup_compileSdk
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion version_setup_minSdk
        targetSdkVersion version_setup_targetSdk

        flavorDimensions "versionCode"
        multiDexEnabled false


        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField "String[]", "DETECTED_ANDROID_LOCALES", "${findUsedAndroidLocales()}"
        buildConfigField "String", "BUILD_DATE", "\"${getBuildDate()}\""
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""
        resValue "string", "manifest_package_id", "com.benny.openlauncher"

        resValue 'string', 'app_name', "OpenLauncher"

        applicationId "com.benny.openlauncher"
        versionCode 35
        versionName "0.5.12"
    }

    productFlavors {
        flavorDefault {
        }

        flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }

        flavorGplayPreAlpha {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
            buildConfigField "boolean", "IS_GPLAY_PREALPHA_BUILD", "true"
            resValue 'string', 'app_name', "OL PreAlpha"

            versionCode = Integer.parseInt(new Date().format('yyMMddhh'))
            versionName = "0.5.8-pre-alpha"
            applicationId "com.benny.openlauncherprealpha"
        }

        flavorTest {
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
            resValue 'string', 'app_name', "OL Nightly"

            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            applicationId "io.github.openlauncherteam.openlauncher.test"
        }
    }

    sourceSets {
        if (enable_plugin_kotlin) {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support') {
                    if (details.requested.name != 'multidex' && details.requested.name != 'multidex-instrumentation') {
                        details.useVersion "${version_library_appcompat}"
                    }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE-LGPL-2.1.txt'
        exclude 'META-INF/LICENSE-LGPL-3.txt'
        exclude 'META-INF/LICENSE-W3C-TEST'
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
    }
}

dependencies {
    // Support libraries
    implementation "com.android.support:recyclerview-v7:${version_library_appcompat}"
    implementation "com.android.support:appcompat-v7:${version_library_appcompat}"
    implementation "com.android.support:cardview-v7:${version_library_appcompat}"
    implementation "com.android.support:design:${version_library_appcompat}"
    implementation "com.android.support:support-v13:${version_library_appcompat}"
    implementation "com.android.support:preference-v7:${version_library_appcompat}"
    implementation "com.android.support:preference-v14:${version_library_appcompat}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    // UI Libs
    implementation 'com.mikepenz:fastadapter:3.2.3@aar'
    implementation 'com.mikepenz:fastadapter-commons:3.0.0@aar'
    implementation 'com.mikepenz:fastadapter-extensions:3.0.0@aar'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.eightbitlab:blurview:1.3.3'
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.balysv:material-ripple:1.0.2'
    implementation 'agency.tango.android:material-intro-screen:0.0.5'
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.jaredrummler:colorpicker:1.0.1'
    implementation 'in.championswimmer:SimpleFingerGestures_Android_Library:1.2'
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    implementation ('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }

    implementation('com.github.turing-tech:MaterialScrollBar:12.4.0') {
        exclude group: 'com.android.support'
    }


    // Tools
    implementation "com.jakewharton:butterknife:${version_library_butterknife}"
    if (enable_plugin_kotlin) {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$version_plugin_kotlin"
    }

    // Processors
    def anpros = ["com.jakewharton:butterknife-compiler:${version_library_butterknife}"]
    for (anpro in anpros) {
        if (enable_plugin_kotlin) {
            kapt anpro
        } else {
            annotationProcessor anpro
        }
    }

}
repositories {
    mavenCentral()
}
